set(TARGET builtin_interfaces)
set(msg_files
  "msg/Duration.msg"
  "msg/Time.msg"
)
list(TRANSFORM msg_files
  PREPEND "${ZEPHYR_CURRENT_MODULE_DIR}/${TARGET}:"
  OUTPUT_VARIABLE non_idl_tuples)

set(output_files "")
set(output_typesupport "")
set(output_typesupport_microxrcedds "")
foreach(msg ${msg_files})
  string_camel_case_to_lower_case_underscore(${msg} msg_lower)
  cmake_path(GET msg_lower STEM stem)
  cmake_path(GET msg_lower PARENT_PATH parent_path)
  # generated by idl_to_files
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/${stem}.h")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__functions.h")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__functions.c")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__struct.h")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__type_support.h")
  # generated by idl_to_typesupport
  list(APPEND output_typesupport "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/${stem}__type_support.cpp")
  # generated by idl_to_typesupport_microxrcedds
  list(APPEND output_typesupport_microxrcedds "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__rosidl_typesupport_microxrcedds_c.h")
  list(APPEND output_typesupport_microxrcedds "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/microxrcedds/${stem}__type_support_c.c")
endforeach()

gen_files(
  TARGET ${TARGET}
  NON_IDL_TUPLES ${non_idl_tuples}
  OUTPUT_FILES ${output_files}
  OUTPUT_TYPESUPPORT ${output_typesupport}
  OUTPUT_TYPESUPPORT_MICROXRCEDDS ${output_typesupport_microxrcedds}
)

add_custom_target(ros2_gen_builtin_interfaces_headers
  DEPENDS ${output_files}
)

zephyr_interface_library_named(ros2_builtin_interfaces)
add_dependencies(ros2_builtin_interfaces ros2_gen_builtin_interfaces_headers)
target_include_directories(ros2_builtin_interfaces INTERFACE ${CMAKE_CURRENT_BINARY_DIR}/include)
