set(TARGET std_msgs)
set(msg_files
  "msg/Empty.msg"
  "msg/Bool.msg"
  "msg/Byte.msg"
  "msg/Char.msg"
  "msg/String.msg"
  "msg/Int8.msg"
  "msg/Int16.msg"
  "msg/Int32.msg"
  "msg/Int64.msg"
  "msg/UInt8.msg"
  "msg/UInt16.msg"
  "msg/UInt32.msg"
  "msg/UInt64.msg"
  "msg/Float32.msg"
  "msg/Float64.msg"
  "msg/ByteMultiArray.msg"
  "msg/Int8MultiArray.msg"
  "msg/Int16MultiArray.msg"
  "msg/Int32MultiArray.msg"
  "msg/Int64MultiArray.msg"
  "msg/UInt8MultiArray.msg"
  "msg/UInt16MultiArray.msg"
  "msg/UInt32MultiArray.msg"
  "msg/UInt64MultiArray.msg"
  "msg/Float32MultiArray.msg"
  "msg/Float64MultiArray.msg"
  "msg/MultiArrayDimension.msg"
  "msg/MultiArrayLayout.msg"
  "msg/Header.msg"
  "msg/ColorRGBA.msg"
)

list(TRANSFORM msg_files
  PREPEND "${ZEPHYR_CURRENT_MODULE_DIR}/${TARGET}:"
  OUTPUT_VARIABLE non_idl_tuples)

set(output_files "")
set(output_typesupport "")
set(output_typesupport_microxrcedds "")
foreach(msg ${msg_files})
  string_camel_case_to_lower_case_underscore(${msg} msg_lower)
  cmake_path(GET msg_lower STEM stem)
  cmake_path(GET msg_lower PARENT_PATH parent_path)
  # generated by idl_to_files
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/${stem}.h")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__functions.h")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__functions.c")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__struct.h")
  list(APPEND output_files "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__type_support.h")
  # generated by idl_to_typesupport
  list(APPEND output_typesupport "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/${stem}__type_support.cpp")
  # generated by idl_to_typesupport_microxrcedds
  list(APPEND output_typesupport_microxrcedds "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/${stem}__rosidl_typesupport_microxrcedds_c.h")
  list(APPEND output_typesupport_microxrcedds "${CMAKE_CURRENT_BINARY_DIR}/include/${TARGET}/${parent_path}/detail/microxrcedds/${stem}__type_support_c.c")
endforeach()

gen_files(
  TARGET ${TARGET}
  NON_IDL_TUPLES ${non_idl_tuples}
  OUTPUT_FILES ${output_files}
  OUTPUT_TYPESUPPORT ${output_typesupport}
  OUTPUT_TYPESUPPORT_MICROXRCEDDS ${output_typesupport_microxrcedds}
)

add_custom_target(ros2_gen_std_msgs_headers
  DEPENDS ${output_files}
)

list(FILTER output_files INCLUDE REGEX "\\.(c|cpp)$")
list(FILTER output_typesupport INCLUDE REGEX "\\.(c|cpp)$")
list(FILTER output_typesupport_microxrcedds INCLUDE REGEX "\\.(c|cpp)$")

zephyr_library_named(ros2_std_msgs)
add_dependencies(ros2_std_msgs ros2_gen_std_msgs_headers)
zephyr_library_include_directories(PUBLIC ${CMAKE_CURRENT_BINARY_DIR}/include)
zephyr_library_link_libraries(
  ros2_rosidl_runtime_c
  ros2_rosidl_typesupport_microxrcedds_c
  ros2_microcdr
  ros2_builtin_interfaces
  ros2_rosidl_typesupport_c
)
zephyr_library_sources(
  ${output_files}
  ${output_typesupport}
  ${output_typesupport_microxrcedds})
